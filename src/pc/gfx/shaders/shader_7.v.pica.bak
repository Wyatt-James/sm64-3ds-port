; texture, fog, 1 color rgba
; disabled for fog. Uses shader_5 instead.

; Uniforms
.fvec projection_mtx[4], model_view_mtx[4], game_projection_mtx[4]
.fvec tex_scale
.bool draw_fog

; Constants
.constf myconst(0.0, 1.0, -1.0, -0.5)
.constf tcofs(0.0, 1.0, 0.0, 0.0)

.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones

; Inputs
.in inpos v0 ; xyz, plus garbage for w
.in intex v1 ; uv
.in infog v2
.in inclr v3

; Outputs
.out outpos position  ; xyzw
.out outtc0 texcoord0 ; texture unit 0
.out outtc1 texcoord1 ; texture unit 1
.out outclr color     ; rgba

.proc main

setup:
    mov r0, inpos
model_view:
    ; r1 = model_view_mtx * inpos
    dphi r1.x, r0, model_view_mtx[0]
    dphi r1.y, r0, model_view_mtx[1]
    dphi r1.z, r0, model_view_mtx[2]
    dphi r1.w, r0, model_view_mtx[3]
game_projection:
    ; r0 = game_projection_mtx * inpos
    dp4 r0.x, game_projection_mtx[0], r1
    dp4 r0.y, game_projection_mtx[1], r1
    dp4 r0.z, game_projection_mtx[2], r1
    dp4 r0.w, game_projection_mtx[3], r1
system_projection:
    ; outpos = projection_mtx * inpos
    dp4 outpos.x, projection_mtx[0], r0
    dp4 outpos.y, projection_mtx[1], r0
    dp4 outpos.z, projection_mtx[2], r0
    dp4 outpos.w, projection_mtx[3], r0
texture:
    ; outtex = intex
    mul r2, tex_scale, intex
    add outtc0, tcofs, r2
    mov outtc1, outtc0
color:
    ifu draw_fog
        mov outclr, infog
    .else
        ; copy color
        mov outclr, inclr
    .end
    ; We're finished
    end
.end
