; position, texture, color rgb

; Uniforms
.fvec projection[4], modelView[4]
.fvec tex_scale

; Constants
.constf myconst(0.0, 1.0, -1.0, -0.5)
.constf tcofs(0.0, 1.0, 0.0, 0.0)

.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones

; Inputs
.in inpos v0 ; xyz, plus garbage for w
.in intex v1 ; uv
.in inclr v2

; Outputs
.out outpos position  ; xyzw
.out outtc0 texcoord0 ; texture unit 0
.out outtc1 texcoord1 ; texture unit 1
.out outclr color     ; rgba

.proc main

setup:
    mov r0, inpos
model:
    ; r1 = modelView * inpos
    dphi r1.x, r0, modelView[0]
    dphi r1.y, r0, modelView[1]
    dphi r1.z, r0, modelView[2]
    dphi r1.w, r0, modelView[3]
projection:
    ; outpos = projection * inpos
    dp4 outpos.x, projection[0], r1
    dp4 outpos.y, projection[1], r1
    dp4 outpos.z, projection[2], r1
    dp4 outpos.w, projection[3], r1
texture:
    ; outtex = intex
    mul r2, tex_scale, intex
    add outtc0, tcofs, r2
    mov outtc1, outtc0
color:
    ; copy color
    mov outclr.xyz, inclr.xyz
    mov outclr.w, ones

    ; We're finished
    end
.end
